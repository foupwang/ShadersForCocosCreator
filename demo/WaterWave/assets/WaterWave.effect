// Effect Syntax Guide: https://docs.cocos.com/creator/manual/zh/shader/index.html

CCEffect %{
  techniques:
  - passes:
    - vert: sprite-vs:vert
      frag: sprite-fs:frag
      depthStencilState:
        depthTest: false
        depthWrite: false
      blendState:
        targets:
        - blend: true
        blendSrc: src_alpha
        blendDst: one_minus_src_alpha
        blendDstAlpha: one_minus_src_alpha
      rasterizerState:
        cullMode: none
      properties:
        u_size: { value: [0.0, 0.0] }
        u_time: { value: 0.0 }
        u_rate: { value: 0.2 }
}%

CCProgram sprite-vs %{
  precision highp float;

  #include <builtin/uniforms/cc-global>
  
  in vec3 a_position;
  in vec2 a_texCoord;
  in vec4 a_color;

  out vec4 v_color;
  out vec2 v_uv0;

  vec4 vert () {
    vec4 pos = vec4(a_position, 1);
    pos = cc_matViewProj * pos;
    v_uv0 = a_texCoord;
    v_color = a_color;
    
    return pos;
  }
}%


CCProgram sprite-fs %{
  precision highp float;
  #include <builtin/internal/embedded-alpha>
  #include <builtin/internal/alpha-test>

  in vec4 v_color;
  in vec2 v_uv0;

  uniform Constant {
    vec2 u_size;
    float u_time;
    float u_rate;
  };

  #if USE_TEXTURE
    #pragma builtin(local)
    layout(set = 2, binding = 12) uniform sampler2D cc_spriteTexture;
  #endif

  // 计算出波浪效果的新坐标
  vec2 wave(in vec2 p) {
      float range = 8.0; // 波浪效果的起伏幅度
      float time = u_time * u_rate;

      // 将p和time相加，目的是让顶点跟随时间的变化而不断移动
      float x = range * (p.x + time);
      float y = range * (p.y + time);
      
      // cos(x-y)*cos(y) 可以让波浪呈现出沿对角线方向传播的效果
      // 自然界中，波浪通常会有一个中心位置，并且在这个位置附近会呈现出周期性的变化。
      // sin(x+y)*sin(y) 可以让波浪的变化与中心位置关联起来，产生更自然流畅的波浪效果。
      vec2 uv = vec2(cos(x-y)*cos(y), sin(x+y)*sin(y));
      return uv;
  }

  vec4 frag () {
    vec4 o = vec4(1, 1, 1, 1);

    #if USE_TEXTURE
      // 得到波浪效果的新坐标
      vec2 new_uv = wave(v_uv0) - wave(v_uv0+u_size);
      // 2.0/u_size.x用来缩放波浪大小，控制水面形状
      vec2 uv = v_uv0 + 2.0 / u_size.x * new_uv;
      o *= CCSampleWithAlphaSeparated(cc_spriteTexture, uv);
    #endif

    o *= v_color;
    ALPHA_TEST(o);
    return o;
  }
}%
